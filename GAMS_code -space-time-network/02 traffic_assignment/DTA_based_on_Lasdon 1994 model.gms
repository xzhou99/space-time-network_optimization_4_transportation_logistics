$Lasdon, L. S., & Luo, S. (1994). Computational experiments with a system optimal dynamic traffic assignment model. Transportation Research Part C: $Emerging Technologies, 2(2), 109-127.

$TITLE DYNAMIC TRAFFIC ASSIGNMENT PROBLEM
$INLINECOM { }
OPTION ITERLIM = 1000 ;
OPTION RESLIM = 99999 ;
OPTION NLP = CONOPT ;
SET A ARC INDEX /A1*A20/ ;
SET J O-D INDEX /OD1*OD3/ ;
SET N NODE INDEX /N1*N8/;
SET T TIME PERIOD INDEX /T1*T30/ ;
PARAMETERS NUMNODES, NUMARCS, NUMODS, NUMPERI ;
NUMPERI = 30 ;
PARAMETERS VT(T) VALUE FOR T ;
VT(T) = ORD(T) ;
PARAMETERS INITINV(A,J) INITIAL INVENTORY OF OD PAIR ON ARC A ;
INITINV(A,J) = 0 ;
TABLE ODESIRE(T,J) O-D DESIRES FOR PAIR J IN GIVEN INTERVAL T
        OD1  OD2  OD3
T1*T5   125  125  125
T6*T10  250  250  250
T11*T15 500  500  500
T16*T20 375  375  375
T21*T25 125  125  125
T26*T30 0    0    0

SETS ORIDEST(J,N) ORIGIN AND DESTINATION NODES FOR OD PAIR J ;
ORIDEST(J,N) = NO ;
ALIAS (A, AP) ;
ALIAS (T, TP) ;
SET AFTER(N,A); AFTER(N,A)= NO ;
SET BEFORE(A,N) ; BEFORE(A,N) = NO ;
SET ORIARC(J,A) ARCS OUT OF ORIGIN OF OD PAIR J ;
ORIARC(J,A) = NO ;
SET ARCORI(J,A) ARCS INTO THE ORIGIN OF OD PAIR J ;
ARCORI(J,A) = NO ;
SET ARCDEST(J,A) ARCS INTO THE DESTINATION OF OD PAIR J ;
ARCDEST(J,A) = NO ;
SET DESTARC(J,A) ARCS OUT OF THE DESTINATION OF OD PAIR J ;
DESTARC(J,A) = NO ;
SET ATTR ATTRIBUTES OF AN ARC
/VMIN, VMAX, LEN, CAP/;
PARAMETERS AI(A,ATTR) INFORMATION FOR ARC A (ARC INFORMATION) ;
* VMIN, VMAX ARE IN MILES/MINUTE, LEN IS MILES, CAP IS # OF VEHICLES
NUMODS = 3 ;
NUMNODES=8;
ORIDEST("OD1","N2")=YES ; ORIDEST("OD1","N6")=YES;
ORIDEST("OD2","N6") = YES ; ORIDEST("OD2","N2") = YES ;
ORIDEST("OD3","N8") = YES ; ORIDEST("OD3","N3") = YES ;

AFTER("N1","A1")=YES; AI("A1","LEN")=0.50;
AI("A1","CAP") = 260.00 ;AI("A1","VMAX")= 0.50 ;
BEFORE("A1","N2") = YES ;
ARCORI("OD1","A1")=YES;
ARCDEST("OD2","A1")=YES;

AFTER("N1","A2")= YES; AI("A2","LEN") = 0.50 ;
AI("A2","CAP") = 260.00 ; AI("A2","VMAX") = 0.50 ;
BEFORE("A2","N4") = YES ;

AFTER("N2","A3") = YES ; AI("A3","LEN") = 0.50 ;
AI("A3","CAP") = 260.00 ; AI("A3","VMAX") = 0.50 ;
BEFORE("A3","N1")= YES ;
ORIARC("OD1","A3")= YES ;
DESTARC("OD2","A3") = YES ;

AFTER("N2","A4") = YES ; AI("A4","LEN") = 0.50 ;
AI("A4","CAP") = 260.00 ; AI("A4","VMAX") = 0.75 ;
BEFORE("A4","N5") = YES ;
ORIARC("OD1","A4")=YES;
DESTARC("OD2" ,"A4") = YES ;

AFTER("N3","A5") = YES ; AI("A5","LEN") = 0.50 ;
AI("A5","CAP") = 260.00 ;AI("A5","VMAX")= 0.75 ;
BEFORE("A5","N4") = YES ;
DESTARC("OD3","A5") = YES ;

AFTER("N3","A6") = YES ; AI("A6","LEN") = 0.50 ;
AI("A6","CAP") = 260.00 ; AI("A6","VMAX") = 0.50 ;
BEFORE("A6","N6") = YES ;
ARCDEST("OD1","A6") = YES ;
ARCORI("OD2","A6") = YES ;
DESTARC("OD3","A6") = YES ;

AFTER("N4","A7") = YES ; AI("A7","LEN") = 0.50 ;
AI("A7","CAP") = 260.00 ; AI("A7","VMAX") = 0.50 ;
BEFORE("A7","N1")= YES ;

AFTER("N4","A8") = YES ; AI("A8","LEN") = 0.50 ;
AI("A8","CAP") = 260.00 ; AI("A8","VMAX") = 0.75 ;
BEFORE("A8","N3") = YES ;
ARCDEST("OD3","A8") = YES ;

AFTER("N4","A7") = YES ; AI("A7","LEN") = 0.50 ;
AI("A7","CAP") = 260.00 ; AI("A7","VMAX") = 0.50 ;
BEFORE("A7","N1") = YES ;

AFTER("N4","A9") = YES ; AI("A9","LEN") = 0.50 ;
AI("A9","CAP") = 260.00 ; AI("A9","VMAX") = 0.75 ;
BEFORE("A9","N5") = YES ;

AFTER("N4","A10") = YES ; AI("A10","LEN") = 0.50 ;
AI("A10","CAP") = 260.00 ; AI("A10","VMAX") = 0.75 ;
BEFORE("A10","N7") = YES ;

AFTER("N5","A11") = YES ; AI("A11","LEN") = 0.50 ;
AI("A11","CAP") = 260.00 ; AI("A11","VMAX") = 0.75 ;
BEFORE("A11","N2") = YES ;
ARCDEST("OD2","A11")=YES;
ARCORI("OD1","A11")=YES;

AFTER("N5","A12") = YES ; AI("A12","LEN") = 0.50 ;
AI("A12","CAP") = 260.00 ; AI("A12","VMAX") = 0.75 ;
BEFORE("A11","N4") = YES ;

AFTER("N5","A13") = YES ; AI("A13","LEN") = 0.50 ;
AI("A13","CAP") = 260.00 ; AI("A13","VMAX") = 0.50 ;
BEFORE("A13","N8") = YES ;
ARCORI("OD3","A13")=YES;

AFTER("N6","A14") = YES ; AI("A14","LEN") = 0.50 ;
AI("A14","CAP") = 260.00 ; AI("A14","VMAX") = 0.50 ;
BEFORE("A14","N3") = YES ;
ORIARC("OD2","A14")=YES;
DESTARC("OD1","A14")=YES;
ARCDEST("OD3","A14")=YES;

AFTER("N6","A15") = YES ; AI("A15","LEN") = 0.50 ;
AI("A15","CAP") = 260.00 ; AI("A15","VMAX") = 0.75 ;
BEFORE("A15","N7") = YES ;
ORIARC("OD2","A15")=YES;
DESTARC("OD1","A15")=YES;

AFTER("N7","A16") = YES ; AI("A16","LEN") = 0.50 ;
AI("A16","CAP") = 260.00 ; AI("A16","VMAX") = 0.75 ;
BEFORE("A14","N4") = YES ;

AFTER("N7","A17") = YES ; AI("A17","LEN") = 0.50 ;
AI("A17","CAP") = 260.00 ; AI("A17","VMAX") = 0.75 ;
BEFORE("A17","N6") = YES ;
ARCORI("OD2","A17")=YES;
ARCDEST("OD1","A17")=YES;

AFTER("N7","A18") = YES ; AI("A18","LEN") = 0.50 ;
AI("A18","CAP") = 260.00 ; AI("A18","VMAX") = 0.50 ;
BEFORE("A18","N8") = YES ;
ARCORI("OD3","A18")=YES;

AFTER("N8","A19") = YES ; AI("A19","LEN") = 0.50 ;
AI("A19","CAP") = 260.00 ; AI("A19","VMAX") = 0.50 ;
BEFORE("A14","N5") = YES ;
ORIARC("OD3","A19")=YES;

AFTER("N8","A20") = YES ; AI("A20","LEN") = 0.50 ;
AI("A20","CAP") = 260.00 ; AI("A20","VMAX") = 0.50 ;
BEFORE("A20","N7") = YES ;
ORIARC("OD3","A20")=YES;
AI(A,'VMIN') = 0.1 ;
SCALARS
PEN PENALTY COEFFICIENT /99/
DEL SIZE OF TIME INTERVAL {MIN} /0.5/
VMAX MAX SPEED ON ALL ARCS /36/
VMIN MIN SPEED ON ALL ARCS /15/
ALPHA EXPONENT IN EQUATION GIVING VELOCITY AS A FUNCTION OF VEHICLE CONCENTRATION /1/ ;
POSITIVE VARIABLES
X(T,J,A) NO. OF VEHICLES (J) ON ARC A AT THE END OF PERIOD T
CX(T,A) TOTAL NO. OF VEHICLES ON ARC A AT THE END OF PERIOD T
E(T,J,A) NO. OF VEHICLES (J)ENTERING ARC A DURING T
L(T,J,A) NO. OF VEHICLES (J)LEAVING ARC A DURING T
DN(J) DEVIATION VARIABLE FOR DEMAND CONSTRAINTS
Q(T,J) TYPE J VEHICLES THAT CANNOT LEAVE AT PERIOD T ;
VARIABLES
TT TOTAL TRAVEL TIME {OBJECTIVE FUNCTION} ;
EQUATIONS
INVBAL(T,J,A) ARC VEHICLE INVENTORY BALANCE
LEAVELIM(T,J,A) INITIAL INVENTORY UPPER BOUND ON LEAVING VEHICLES
INTCON(T,J,N) VEHICLE FLOW CONSERVATION AT INTERMEDIATE NODES
ORICON(T,J) VEHICLE FLOW CONSERVATION AT ORIGIN NODES
DESTCON(J) VEHICLE FLOW CONSERVATION AT DESTINATION NODES
TOTFLOW(A,T) TOTAL INVENTORY ON ARC A AT END OF TIME T
* NONLINEAR CONSTRAINTS
EXITCAT(A,T) THE NONLINEAR EXIT FUNCTION CONSTRAINTS
TIMELIM(A) AVERAGE TIME CANNOT BE LOWER THAN SHORTEST POSSIBLE
TTIME TOTAL TRAVEL TIME ;

INVBAL(T,J,A) ..
* INVENTORY BALANCE OF VEHICLES ON EACH ARC AT EACH TIME PERIOD FOR EACH
* OD PAIR.
X(T,J,A) =E= X(T-1,J,A) $ (VT(T) GT 1) + INITINV(A,J)$ (VT(T) EQ 1)+
 E(T,J,A) - L(T,J,A) ;

LEAVELIM(T,J,A)..
* MAXIMUM NUMBER OF VEHICLES THAT CAN LEAVE AN ARC.
L(T,J,A) =L= X(T-1,J,A) $ (VT(T) GT 1) +INITINV(A,J) $ (VT(T) EQ 1) ;

INTCON(T,J,N) $ (NOT ORIDEST(J,N)) ..
* AT EACH INTERMEDIATE NODE, FLOW IN EQUALS FLOW OUT AT EACH
* TIME PERIOD FOR EACH OD PAIR.
SUM(A$BEFORE(A,N), L(T,J,A)) =E=SUM(A$AFTER(N,A), E(T,J,A)) ;

ORICON(T,J) ..
* FLOW CONSERVATION AT ORIGIN FOR EACH OD PAIR AT EACH TIME PERIOD.
SUM(A$ORIARC(J,A), E(T,J,A)) + Q(T,J)=E= ODESIRE(T,J) + Q(T-1 ,J) ;

DESTCON(J) ..
* FLOW CONSERVATION AT DESTINATION FOR EACH OD PAIR.
SUM(T,SUM(A$ARCDEST(J,A), L(T,J,A)))=E= SUM(T, ODESIRE(T,J))- DN(J);

TOTFLOW(A,T) ..
* TOTAL FLOW ON AN ARC.
SUM(J, X(T,J,A)) =E= CX(T,A) ;

EXITCAT(A,T) ..
* VEHICLES TRAVEL AT THE SPEED PERMITED BY CONCENTRATION LEVEL AND
* MIN AND MAX SPEED, USE CAT AS EXIT FUNCTION WHICH ASSUMES THAT
* VEHICLES ARE DISTRIBUTED EVENLY ON ARCS AT BEGINNING OF EACH PERIOD.
SUM(J,L(T,J,A)) =L= DEL*(AI(A,'VMIN')+(AI(A,'VMAX')-AI(A,'VMIN'))*
(1-SUM(J, X(T-1 ,J,A)$(VT(T) GT 1)+
INITINV(A,J)$(VT(T) EQ 1))/AI(A, 'CAP')))*
(SUM(J,X(T-1,J,A)$(VT(T) GT 1)
+INITINV(A,J)$(VT(T) EQ 1 ))/AI(A,'LEN')) ;

TIMELIM(A) ..
* AVERAGE TRAVEL TIME CANNOT BE LOWER THAN SHORTEST POSSIBLE
AI(A,'VMAX')*DEL*SUM(T,CX(T,A))=G= AI(A, 'LEN')*SUM((T,J),E(T,J,A)) ;

TTIME..
TT =E= DEL*SUM((T,J,A), X(T,J,A)) +PEN*SUM((T,J), Q(T,J)) +PEN*SUM(J, DN(J)) ;
* SIMPLE BOUNDS AND FIXED VARIABLES
L.UP('T1',J,A) = INITINV(A,J) ;
L.FX(T,J,A) $ ARCORI(J,A) = 0 ;
E.FX(T,J,A) $ DESTARC(J,A) = 0 ;
MODEL NLPDTAP /INVBAL, LEAVELIM, INTCON, ORICON, DESTCON,
TOTFLOW, EXITCAT, TIMELIM, TTIME/;
solve NLPDTAP USING NLP MINIMIZING TT ;
display TT.l,   X.l;
